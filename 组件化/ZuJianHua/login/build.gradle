if (Boolean.valueOf(rootProject.ext.isLoginModule)) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
    apply plugin: 'com.jakewharton.butterknife'
}

android {
    compileSdkVersion 28
    buildToolsVersion "29.0.0"


    defaultConfig {
        // Application模式下设置applicationId
        if (Boolean.valueOf(rootProject.ext.isLoginModule)) {
            applicationId "com.skl.login"
        }
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (Boolean.valueOf(rootProject.ext.isLoginModule)) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {
                // Library模式可以不用配置manifest文件
            }
        }
    }

    // 作为Application需要配置Java编译版本
    if (Boolean.valueOf(rootProject.ext.isLoginModule)) {
        // Butterknife requires Java 8.
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // 公共依赖库
    implementation project(path: ':basemodule')

    // TODO 虽然依赖了公共库，但是有些配置还是要模块内部自己配置
    // 黄油刀
    annotationProcessor rootProject.ext.dependencies["butterknife_compiler"]
    // Arouter路由
    annotationProcessor rootProject.ext.dependencies["arouter_compiler"]
}
